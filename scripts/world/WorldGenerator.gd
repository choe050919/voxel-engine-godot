class_name WorldGenerator
extends Node3D
## WorldGenerator.gd - ë¬´í•œ ì²­í¬ ë¡œë”© ì‹œìŠ¤í…œ
## Phase 2-7: WorkerThreadPool ë©€í‹°ìŠ¤ë ˆë”© + LOD

# ================================
# Debug Logging (Inspectorì—ì„œ í† ê¸€)
# ================================
@export_group("Debug Logging")
@export var log_mesh: bool = false       ## [MESH], [THREAD]
@export var log_collision: bool = false  ## [DIAG] collision, [LAG SPIKE]
@export var log_perf: bool = true        ## [PERF]
@export var log_physics: bool = false    ## [Physics]
@export var log_debug: bool = false      ## [DEBUG]
@export var log_diag: bool = false       ## [DIAG] ê¸°íƒ€

# ================================
# Singleton Instance
# ================================
static var instance: WorldGenerator = null

# ================================
# Noise Settings
# ================================
var noise_base: FastNoiseLite
var noise_mountain: FastNoiseLite

# ================================
# Chunk Management
# ================================
var active_chunks: Dictionary = {}     # Vector2i -> VoxelChunk
var chunk_load_queue: Array = []       # ìƒì„± ëŒ€ê¸° í
var chunk_unload_queue: Array = []     # ì‚­ì œ ëŒ€ê¸° í

var _chunk_cache: Dictionary = {}  # Vector2i -> VoxelChunk (ì‚­ì œ ëŒ€ê¸°)
const MAX_CACHED_CHUNKS: int = 50

# ================================
# Mesh Update Queue (Phase 2-7 WorkerThreadPool)
# ================================
var _mesh_update_queue: Array[VoxelChunk] = []  # ë©”ì‰¬ ê°±ì‹  ëŒ€ê¸° í
var _is_mesh_updating: bool = false             # í˜„ì¬ ìŠ¤ë ˆë“œ ì‘ì—… ì§„í–‰ ì¤‘ ì—¬ë¶€

# ================================
# Phase 2-Optimization: Main Thread Throttling Queues
# ================================
var _chunk_apply_queue: Array[VoxelChunk] = []      # ë©”ì‰¬+ì¶©ëŒì²´ ì ìš© ëŒ€ê¸° í
var _collision_request_queue: Array[VoxelChunk] = []  # ì¶©ëŒì²´ë§Œ ìƒì„± ëŒ€ê¸° í
const CHUNK_APPLIES_PER_FRAME: int = 1              # í”„ë ˆì„ë‹¹ ì ìš©í•  ì²­í¬ ìˆ˜
const COLLISION_CREATES_PER_FRAME: int = 1          # í”„ë ˆì„ë‹¹ ì¶©ëŒì²´ ìƒì„± ìˆ˜

# ================================
# Thread Pool Saturation Prevention (ìŠ¤ë ˆë“œ ê¸°ì•„ ë°©ì§€)
# ================================
const MAX_CONCURRENT_BACKGROUND_TASKS: int = 6      # ë°°ê²½ ì‘ì—… ìµœëŒ€ ë™ì‹œ ì‹¤í–‰ ìˆ˜
var _current_background_tasks: int = 0              # í˜„ì¬ ì‹¤í–‰ ì¤‘ì¸ ë°°ê²½ ì‘ì—… ìˆ˜

# ================================
# Player Tracking
# ================================
var player: Node3D = null
var last_player_chunk: Vector2i = Vector2i(999999, 999999)

# ================================
# Phase 2-9: Dynamic Physics Bubble
# ================================
var _physics_bubble_center: Vector2i = Vector2i(999999, 999999)  # ë¬¼ë¦¬ ë²„ë¸” ì¤‘ì‹¬

# ================================
# Debug: Fall Trap
# ================================
var _has_reported_fall: bool = false  # ë‚™í•˜ ë³´ê³  ì—¬ë¶€ (1íšŒë§Œ)

# ================================
# Performance
# ================================
var chunks_generated_this_frame: int = 0
var _fps_log_timer: float = 0.0  # FPS ë¡œê¹… íƒ€ì´ë¨¸

# ================================
# Benchmark System
# ================================
var _benchmark_time: float = 0.0
var _benchmark_duration: float = 30.0  # 30ì´ˆ í›„ í†µê³„ ì¶œë ¥
var _benchmark_done: bool = false
var _stats_fps: Array[float] = []
var _stats_finalize_ms: Array[float] = []
var _stats_collision_ms: Array[float] = []

# ================================
# Phase 2-8: Initial Loading State
# ================================
var _is_initial_loading: bool = true              # ì´ˆê¸° ë¡œë”© ì¤‘ ì—¬ë¶€
var _initial_chunks_to_load: int = 0              # ë¡œë”©í•´ì•¼ í•  ì´ ì²­í¬ ìˆ˜
var _initial_chunks_finalized: int = 0            # finalize_build ì™„ë£Œëœ ì²­í¬ ìˆ˜
var _loading_screen: LoadingScreen = null         # ë¡œë”© UI ì°¸ì¡°
const INITIAL_LOAD_RADIUS: int = 3                # ì´ˆê¸° ë¡œë”© ë°˜ê²½ (Physics Distance ê¸°ë°˜)

# ã…‡ã…‡
var _priority_apply_queue: Array[VoxelChunk] = []

# ì¸ì ‘ ì²­í¬ ë°©í–¥ (4ë°©í–¥)
const NEIGHBOR_OFFSETS: Array[Vector2i] = [
	Vector2i(1, 0),   # +X
	Vector2i(-1, 0),  # -X
	Vector2i(0, 1),   # +Z
	Vector2i(0, -1),  # -Z
]


func _ready() -> void:
	# Phase 5-2: ì•ˆì „ì¥ì¹˜ - ë©”ì¸ ë©”ë‰´ë¥¼ ê±°ì¹˜ì§€ ì•Šê³  ì‹¤í–‰ë˜ì—ˆì„ ê²½ìš° ë°©ì–´
	if SaveManager.current_world_id == "":
		push_warning("[WorldGenerator] No world loaded. Returning to MainMenu.")
		get_tree().call_deferred("change_scene_to_file", "res://scenes/MainMenu.tscn")
		return

	instance = self
	_setup_noise()
	_setup_initial_loading()
	call_deferred("_find_player")

	# ë¡œê¹… ì„¤ì • ìƒíƒœ ì¶œë ¥
	print("[CONFIG] Logging: mesh=%s, collision=%s, perf=%s, physics=%s, debug=%s, diag=%s" % [
		log_mesh, log_collision, log_perf, log_physics, log_debug, log_diag
	])

func _exit_tree() -> void:
	if instance == self:
		# Phase 5: ì¢…ë£Œ ì‹œ ëª¨ë“  Dirty ì²­í¬ ë™ê¸° ì €ì¥
		SaveManager.flush_dirty_chunks_sync(active_chunks)
		instance = null


func _process(_delta: float) -> void:
	if player == null:
		return

	_update_player_chunk()

	# ì²­í¬ ìƒì„±/ì‚­ì œ ì²˜ë¦¬
	chunks_generated_this_frame = 0
	_process_load_queue()
	_process_unload_queue()

	# â˜… ë©”ì‰¬ ì—…ë°ì´íŠ¸ í ì²˜ë¦¬ (í”„ë ˆì„ë‹¹ ì œí•œ)
	_process_mesh_update_queue()

	# â˜… [Critical Optimization] ì²­í¬ ì ìš© í ì²˜ë¦¬ (í”„ë ˆì„ë‹¹ 1ê°œ)
	_process_chunk_apply_queue()

	# â˜… Phase 2-9: Dynamic Physics Bubble (ì‹¤ì‹œê°„ ë¬¼ë¦¬ ì˜ì—­ ê°±ì‹ )
	_update_physics_bubble()

	# â˜… [Critical Optimization] ì¶©ëŒì²´ ìƒì„± í ì²˜ë¦¬ (í”„ë ˆì„ë‹¹ 1ê°œ)
	_process_collision_request_queue()

	_cleanup_chunk_cache()

	# â˜… [Fall Trap] í”Œë ˆì´ì–´ê°€ ë•…(Y=0) ì•„ë˜ë¡œ ë–¨ì–´ì§€ë©´ ìƒíƒœ ë¤í”„
	if player and player.global_position.y < -10.0 and not _has_reported_fall:
		_has_reported_fall = true
		print("\n!!!!!!!!!! PLAYER FELL THROUGH WORLD !!!!!!!!!!")
		print("[FALL TRAP] Player Position: %s" % player.global_position)
		print("[FALL TRAP] Player Velocity: %s" % player.velocity)
		print("[FALL TRAP] is_locked: %s" % player.is_locked)
		print("[FALL TRAP] is_on_floor: %s" % player.is_on_floor())

		# í”Œë ˆì´ì–´ ì£¼ë³€ ì²­í¬ ìƒíƒœ ë¤í”„
		var player_chunk: Vector2i = Global.world_to_chunk(player.global_position)
		print("[FALL TRAP] Player Chunk: %s" % player_chunk)
		print("[FALL TRAP] Physics Bubble Center: %s" % _physics_bubble_center)

		print("\n[FALL TRAP] === Nearby Chunks Status ===")
		for dx in range(-2, 3):
			for dz in range(-2, 3):
				var check_pos: Vector2i = player_chunk + Vector2i(dx, dz)
				if active_chunks.has(check_pos):
					var chunk: VoxelChunk = active_chunks[check_pos]
					var dist: float = Vector2(player_chunk - check_pos).length()
					print("[FALL TRAP] Chunk %s (dist=%.1f): has_collision=%s, has_physics_body=%s, is_thread_running=%s" % [
						check_pos, dist,
						chunk.has_collision(),
						chunk.has_physics_body(),
						chunk.is_thread_running()
					])
				else:
					print("[FALL TRAP] Chunk %s: NOT LOADED" % check_pos)

		print("\n[FALL TRAP] === Queue Status ===")
		print("[FALL TRAP] Mesh Update Queue: %d" % _mesh_update_queue.size())
		print("[FALL TRAP] Chunk Apply Queue: %d" % _chunk_apply_queue.size())
		print("[FALL TRAP] Collision Request Queue: %d" % _collision_request_queue.size())
		print("[FALL TRAP] Is Mesh Updating: %s" % _is_mesh_updating)
		print("!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!\n")

	# â˜… [PERF] 1ì´ˆë§ˆë‹¤ FPS ë° í ìƒíƒœ ì¶œë ¥
	_fps_log_timer += _delta
	if _fps_log_timer >= 1.0:
		_fps_log_timer = 0.0
		if log_perf:
			print("[PERF] FPS: %d | MeshQ: %d | ApplyQ: %d | ColQ: %d | Chunks: %d" % [
				Engine.get_frames_per_second(),
				_mesh_update_queue.size(),
				_chunk_apply_queue.size(),
				_collision_request_queue.size(),
				active_chunks.size()
			])

	# â˜… [BENCHMARK] 30ì´ˆê°„ ì„±ëŠ¥ ë°ì´í„° ìˆ˜ì§‘
	if not _benchmark_done:
		_benchmark_time += _delta
		_stats_fps.append(Engine.get_frames_per_second())

		if _benchmark_time >= _benchmark_duration:
			_print_benchmark()
			_benchmark_done = true


## ë²¤ì¹˜ë§ˆí¬ ê¸°ë¡ í•¨ìˆ˜
func record_finalize_time(ms: float) -> void:
	if not _benchmark_done:
		_stats_finalize_ms.append(ms)


func record_collision_time(ms: float) -> void:
	if not _benchmark_done:
		_stats_collision_ms.append(ms)


## ë²¤ì¹˜ë§ˆí¬ ê²°ê³¼ ì¶œë ¥
func _print_benchmark() -> void:
	if not log_perf:
		return

	print("\n" + "=".repeat(60))
	print("ğŸ“Š BENCHMARK RESULTS (%.0f seconds)" % _benchmark_duration)
	print("=".repeat(60))

	# FPS í†µê³„
	if _stats_fps.size() > 0:
		var fps_min: int = _stats_fps.min()
		var fps_max: int = _stats_fps.max()
		var fps_sum: int = 0
		for fps in _stats_fps:
			fps_sum += fps
		var fps_avg: float = float(fps_sum) / float(_stats_fps.size())
		print("FPS: min=%d, max=%d, avg=%.1f" % [fps_min, fps_max, fps_avg])
	else:
		print("FPS: no data")

	# finalize_build í†µê³„
	if _stats_finalize_ms.size() > 0:
		var fin_min: float = _stats_finalize_ms.min()
		var fin_max: float = _stats_finalize_ms.max()
		var fin_sum: float = 0.0
		for ms in _stats_finalize_ms:
			fin_sum += ms
		var fin_avg: float = fin_sum / float(_stats_finalize_ms.size())
		print("finalize_build: min=%.1fms, max=%.1fms, avg=%.1fms, count=%d" % [fin_min, fin_max, fin_avg, _stats_finalize_ms.size()])
	else:
		print("finalize_build: no data")

	# collision_create í†µê³„
	if _stats_collision_ms.size() > 0:
		var col_min: float = _stats_collision_ms.min()
		var col_max: float = _stats_collision_ms.max()
		var col_sum: float = 0.0
		for ms in _stats_collision_ms:
			col_sum += ms
		var col_avg: float = col_sum / float(_stats_collision_ms.size())
		print("collision_create: min=%.1fms, max=%.1fms, avg=%.1fms, count=%d" % [col_min, col_max, col_avg, _stats_collision_ms.size()])
	else:
		print("collision_create: no data")

	print("Total chunks: %d" % active_chunks.size())
	print("=".repeat(60) + "\n")


## Phase 2-8: ì´ˆê¸° ë¡œë”© ì„¤ì •
func _setup_initial_loading() -> void:
	_is_initial_loading = true
	_initial_chunks_finalized = 0  # finalize_build ì™„ë£Œ ì¹´ìš´í„°

	# ì´ˆê¸° ë¡œë”©í•  ì²­í¬ ìˆ˜ ê³„ì‚° (ì›í˜• ë°˜ê²½)
	var count: int = 0
	for x in range(-INITIAL_LOAD_RADIUS, INITIAL_LOAD_RADIUS + 1):
		for z in range(-INITIAL_LOAD_RADIUS, INITIAL_LOAD_RADIUS + 1):
			var dist: float = Vector2(x, z).length()
			if dist <= INITIAL_LOAD_RADIUS:
				count += 1

	_initial_chunks_to_load = count

	# ë¡œë”© UI ìƒì„±
	_loading_screen = LoadingScreen.new()
	add_child(_loading_screen)
	_loading_screen.update_progress(0, _initial_chunks_to_load)


## í”Œë ˆì´ì–´ ë…¸ë“œ ì°¾ê¸°
func _find_player() -> void:
	player = get_parent().get_node_or_null("Player")
	if player:
		# Phase 2-8: ì´ˆê¸° ë¡œë”© ì¤‘ì´ë©´ í”Œë ˆì´ì–´ ì ê¸ˆ
		if _is_initial_loading and player.has_method("lock"):
			player.lock()

		last_player_chunk = Vector2i(999999, 999999)
		_update_player_chunk()


## ë…¸ì´ì¦ˆ ì„¤ì •
func _setup_noise() -> void:
	noise_base = FastNoiseLite.new()
	
	# ì €ì¥ ë§¤ë‹ˆì €ì—ì„œ í˜„ì¬ ì›”ë“œì˜ ì‹œë“œë¥¼ ê°€ì ¸ì˜´
	var saved_seed = SaveManager.get_current_seed()
	if saved_seed != 0:
		noise_base.seed = saved_seed
		print("[WorldGenerator] Applied saved seed: %d" % saved_seed)
	else:
		noise_base.seed = randi() # ë¹„ìƒìš©
	
	noise_base.noise_type = FastNoiseLite.TYPE_SIMPLEX_SMOOTH
	noise_base.frequency = 0.01
	noise_base.fractal_octaves = 4
	noise_base.fractal_lacunarity = 2.0
	noise_base.fractal_gain = 0.5

	noise_mountain = FastNoiseLite.new()
	noise_mountain.seed = noise_base.seed + 1000
	noise_mountain.noise_type = FastNoiseLite.TYPE_SIMPLEX_SMOOTH
	noise_mountain.frequency = 0.02
	noise_mountain.fractal_type = FastNoiseLite.FRACTAL_RIDGED
	noise_mountain.fractal_octaves = 5
	noise_mountain.fractal_lacunarity = 2.0
	noise_mountain.fractal_gain = 0.5


# ================================
# Mesh Update Queue System (Phase 2-7 WorkerThreadPool)
# ================================

## ë©”ì‰¬ ì—…ë°ì´íŠ¸ ìš”ì²­ (íì— ì¶”ê°€)
func request_mesh_update(chunk: VoxelChunk) -> void:
	# null ì²´í¬
	if chunk == null:
		return

	# ì´ë¯¸ ì‚­ì œ ì˜ˆì •ì¸ ì²­í¬ëŠ” ë¬´ì‹œ
	if not is_instance_valid(chunk):
		return

	# ì¤‘ë³µ ì²´í¬ (ì´ë¯¸ íì— ìˆìœ¼ë©´ ì¶”ê°€í•˜ì§€ ì•ŠìŒ)
	if chunk in _mesh_update_queue:
		return

	_mesh_update_queue.append(chunk)


## ë©”ì‰¬ ì—…ë°ì´íŠ¸ í ì²˜ë¦¬ (Phase 2-7: WorkerThreadPool)
func _process_mesh_update_queue() -> void:
	# ì´ë¯¸ ìŠ¤ë ˆë“œ ì‘ì—… ì§„í–‰ ì¤‘ì´ë©´ ìŠ¤í‚µ
	if _is_mesh_updating:
		return

	# íê°€ ë¹„ì–´ìˆìœ¼ë©´ ìŠ¤í‚µ
	if _mesh_update_queue.is_empty():
		return

	# ë‹¤ìŒ ì²­í¬ ê°€ì ¸ì˜¤ê¸°
	var chunk: VoxelChunk = _mesh_update_queue.pop_front()

	# ìœ íš¨ì„± ê²€ì‚¬ (ì²­í¬ê°€ ì‚­ì œë˜ì—ˆì„ ìˆ˜ ìˆìŒ)
	if not is_instance_valid(chunk):
		return

	# ì²­í¬ê°€ ì•„ì§ active_chunksì— ìˆëŠ”ì§€ í™•ì¸
	if not active_chunks.has(chunk.chunk_position):
		return

	# ì´ë¯¸ ìŠ¤ë ˆë“œ ì‘ì—… ì¤‘ì¸ ì²­í¬ëŠ” ìŠ¤í‚µ
	if chunk.is_thread_running():
		_mesh_update_queue.append(chunk)  # ë‹¤ì‹œ íì— ë„£ê¸°
		return

	# ìŠ¤ë ˆë“œ ê¸°ë°˜ ë©”ì‰¬ ì—…ë°ì´íŠ¸ ì‹œì‘
	_start_threaded_mesh_update(chunk)


## Phase 2-7: ìŠ¤ë ˆë“œ ê¸°ë°˜ ë©”ì‰¬ ì—…ë°ì´íŠ¸ ì‹œì‘
func _start_threaded_mesh_update(chunk: VoxelChunk) -> void:
	_is_mesh_updating = true

	# ë¬¼ë¦¬ ê±°ë¦¬ ì²´í¬í•˜ì—¬ ì¶©ëŒì²´ í•„ìš” ì—¬ë¶€ ê²°ì •
	var dist: float = Vector2(last_player_chunk - chunk.chunk_position).length()
	var needs_collision: bool = dist <= Global.PHYSICS_DISTANCE

	# ì™„ë£Œ ì‹œê·¸ë„ ì—°ê²° (ì¼íšŒì„±)
	if not chunk.mesh_thread_completed.is_connected(_on_chunk_mesh_thread_completed):
		chunk.mesh_thread_completed.connect(_on_chunk_mesh_thread_completed.bind(chunk), CONNECT_ONE_SHOT)

	# ìŠ¤ë ˆë“œ ì‘ì—… ì‹œì‘
	chunk.start_threaded_mesh_update(needs_collision)


## ì²­í¬ ë©”ì‰¬ ìŠ¤ë ˆë“œ ì™„ë£Œ ì½œë°±
func _on_chunk_mesh_thread_completed(chunk: VoxelChunk) -> void:
	_is_mesh_updating = false

	# ìœ íš¨ì„± ê²€ì‚¬
	if not is_instance_valid(chunk):
		return

	# ë¬¼ë¦¬ ê±°ë¦¬ ë°–ì´ë©´ ì¶©ëŒì²´ ì œê±° (ìƒíƒœ ë³€ê²½ ëŒ€ì‘)
	if active_chunks.has(chunk.chunk_position):
		var dist: float = Vector2(last_player_chunk - chunk.chunk_position).length()
		if dist > Global.PHYSICS_DISTANCE:
			chunk.disable_collision()

	# Note: ì´ˆê¸° ë¡œë”© ì¹´ìš´íŠ¸ëŠ” _create_chunkì—ì„œë§Œ ì¦ê°€
	# (ë©”ì‰¬ ì—…ë°ì´íŠ¸ëŠ” ê¸°ì¡´ ì²­í¬ ê°±ì‹ ì´ë¯€ë¡œ ì¹´ìš´íŠ¸í•˜ì§€ ì•ŠìŒ)


## â˜… ë°°ê²½ ì‘ì—… ì™„ë£Œ ì½œë°± (ìŠ¤ë ˆë“œ í’€ ì—¬ìœ  í™•ë³´)
func _on_background_task_completed() -> void:
	if _current_background_tasks > 0:
		_current_background_tasks -= 1
	
	# ëŒ€ê¸° ì¤‘ì¸ ì‘ì—…ì´ ìˆìœ¼ë©´ ì¦‰ì‹œ ì²˜ë¦¬ ì‹œë„
	if chunk_load_queue.size() > 0:
		_process_load_queue()


# ================================
# Phase 2-Optimization: Chunk Apply Queue (Main Thread Throttling)
# ================================

## VoxelChunkê°€ ìŠ¤ë ˆë“œ ì™„ë£Œ í›„ í˜¸ì¶œí•˜ëŠ” ì ìš© ìš”ì²­ í•¨ìˆ˜
func request_chunk_apply(chunk: VoxelChunk) -> void:
	if not is_instance_valid(chunk):
		return

	# ì¤‘ë³µ ë°©ì§€
	if chunk in _chunk_apply_queue:
		return

	_chunk_apply_queue.append(chunk)

## ì²­í¬ ì ìš© í ì²˜ë¦¬
func _process_chunk_apply_queue() -> void:
	var frame_start: int = Time.get_ticks_usec()

	# â˜… ìš°ì„ ìˆœìœ„ í: ë¬´ì¡°ê±´ ëª¨ë‘ ì²˜ë¦¬ (íƒ€ì„ ë²„ì§“ ë¬´ì‹œ)
	while not _priority_apply_queue.is_empty():
		var chunk = _priority_apply_queue.pop_front()
		if is_instance_valid(chunk) and chunk.is_inside_tree() and active_chunks.has(chunk.chunk_position):
			if chunk.has_method("finalize_build"):
				chunk.finalize_build()

	if _chunk_apply_queue.is_empty():
		return

	# â˜… ì´ˆê¸° ë¡œë”©: í ì „ì²´ë¥¼ í•œ í”„ë ˆì„ì— ì²˜ë¦¬ (íƒ€ì„ ë²„ì§“ ë¬´ì‹œ)
	# â˜… ì¼ë°˜ ìƒíƒœ: í”„ë ˆì„ë‹¹ 1ê°œ, 8ms íƒ€ì„ ë²„ì§“ ì ìš©
	var process_count: int = _chunk_apply_queue.size() if _is_initial_loading else 1

	for i in range(process_count):
		# â˜… íƒ€ì„ ë²„ì§“ ì²´í¬ - ì´ˆê¸° ë¡œë”© ì‹œ ê±´ë„ˆëœ€
		if not _is_initial_loading:
			if Time.get_ticks_usec() - frame_start > 8000:
				if log_perf:
					print("[TIME_BUDGET] Chunk apply paused at %d Âµs" % (Time.get_ticks_usec() - frame_start))
				break
		if _chunk_apply_queue.is_empty():
			break
		var chunk = _chunk_apply_queue.pop_front()
		if not is_instance_valid(chunk):
			continue
		if not chunk.is_inside_tree():
			continue
		if not active_chunks.has(chunk.chunk_position):
			continue
		if chunk.has_method("finalize_build"):
			chunk.finalize_build()
		if _is_initial_loading:
			_initial_chunks_finalized += 1
	
	if _is_initial_loading:
		# ë¡œë”© ì§„í–‰ë¥  ì—…ë°ì´íŠ¸
		if _loading_screen and is_instance_valid(_loading_screen):
			_loading_screen.update_progress(_initial_chunks_finalized, _initial_chunks_to_load)

		# â˜… ì´ˆê¸° ì²­í¬ ìˆ˜ë§Œí¼ finalize ì™„ë£Œë˜ë©´ ì¦‰ì‹œ ë¡œë”© ì¢…ë£Œ
		# ë‚˜ë¨¸ì§€ ì²­í¬ëŠ” ê²Œì„ ì¤‘ ë°°ê²½ì—ì„œ ë¡œë“œ
		if _initial_chunks_finalized >= _initial_chunks_to_load:
			if log_diag:
				print("[DIAG] Initial chunks finalized: %d/%d. Starting game!" % [
					_initial_chunks_finalized, _initial_chunks_to_load
				])
			_finish_initial_loading()


## ì¶©ëŒì²´ ìƒì„± ìš”ì²­ (íì— ì¶”ê°€)
func request_collision_create(chunk: VoxelChunk) -> void:
	if not is_instance_valid(chunk):
		return
	if chunk not in _collision_request_queue:
		_collision_request_queue.append(chunk)

## ì¶©ëŒì²´ ìƒì„± ìš”ì²­ í ì²˜ë¦¬ (í”„ë ˆì„ë‹¹ 1ê°œë§Œ - Physics Bubbleìš©)
func _process_collision_request_queue() -> void:
	if _collision_request_queue.is_empty():
		return

	# í”„ë ˆì„ë‹¹ COLLISION_CREATES_PER_FRAME ê°œë§Œ ì²˜ë¦¬
	for i in range(COLLISION_CREATES_PER_FRAME):
		if _collision_request_queue.is_empty():
			break

		var chunk: VoxelChunk = _collision_request_queue.pop_front()

		# ìœ íš¨ì„± ê²€ì‚¬
		if not is_instance_valid(chunk):
			continue

		if not chunk.is_inside_tree():
			continue

		if not active_chunks.has(chunk.chunk_position):
			continue

		# ì¶©ëŒì²´ ìƒì„± (ìºì‹œëœ ì‰ì´í”„ ì‚¬ìš©)
		chunk.create_collision_robust()


# ================================
# Phase 2-9: Dynamic Physics Bubble
# ================================

## í”Œë ˆì´ì–´ ìœ„ì¹˜ ê¸°ë°˜ ë¬¼ë¦¬ ë²„ë¸” ì—…ë°ì´íŠ¸
func _update_physics_bubble() -> void:
	if not player:
		return

	# ì´ˆê¸° ë¡œë”© ì¤‘ì—ëŠ” ìŠ¤í‚µ (ë³„ë„ ì²˜ë¦¬)
	if _is_initial_loading:
		return

	# í”Œë ˆì´ì–´ì˜ í˜„ì¬ ì²­í¬ ì¢Œí‘œ ê³„ì‚°
	var current_player_chunk: Vector2i = Global.world_to_chunk(player.global_position)

	# ì²­í¬ ê²½ê³„ë¥¼ ë„˜ì—ˆëŠ”ì§€ í™•ì¸ (Change Detection)
	if current_player_chunk == _physics_bubble_center:
		return  # ë³€í™” ì—†ìŒ

	if log_physics:
		print("[Physics] Player moved to chunk %s. Updating bubble..." % current_player_chunk)
	_physics_bubble_center = current_player_chunk

	# ëª¨ë“  í™œì„± ì²­í¬ì˜ ë¬¼ë¦¬ ìƒíƒœ ì¬ê³„ì‚°
	_recalculate_physics_states(current_player_chunk)


func _recalculate_physics_states(center: Vector2i) -> void:
	for chunk_pos in active_chunks.keys():
		var chunk: VoxelChunk = active_chunks[chunk_pos]
		
		# [í•µì‹¬] í”Œë ˆì´ì–´ê°€ ì„œ ìˆëŠ” ì²­í¬ëŠ” ë¬´ì¡°ê±´ í™œì„±í™” ìƒíƒœ ìœ ì§€ 
		if chunk_pos == center:
			chunk.enable_collision()
			continue
			
		var dist: float = Vector2(center - chunk_pos).length()
		var should_have_collision: bool = dist <= Global.PHYSICS_DISTANCE
		
		if should_have_collision:
			chunk.enable_collision()
		else:
			chunk.disable_collision()

# ================================
# Phase 2-8: Initial Loading Management
# ================================

## ì´ˆê¸° ë¡œë”© ì™„ë£Œ ì²˜ë¦¬
## ì´ˆê¸° ë¡œë”© ì™„ë£Œ ì²˜ë¦¬ (ìˆ˜ì •ë³¸)
func _finish_initial_loading() -> void:
	VoxelChunk.prewarm_pool(200)

	_is_initial_loading = false
	if log_diag:
		print("=" .repeat(60))
		print("[DIAG] ===== INITIAL LOADING FINISH SEQUENCE =====")

	# 1. [FIX] í”Œë ˆì´ì–´ ë°ì´í„° ë¨¼ì € ë¡œë“œ (ìœ„ì¹˜ íŒŒì•…ì„ ìœ„í•´)
	var target_chunk_pos := Vector2i(0, 0)
	var player_data := {}
	
	if SaveManager.current_world_id != "":
		player_data = SaveManager.get_player_data_from_world_meta()
		if not player_data.is_empty() and player_data.has("position"):
			var pos_dict = player_data["position"]
			var pos_vec = Vector3(pos_dict.x, pos_dict.y, pos_dict.z)
			# í”Œë ˆì´ì–´ê°€ ë¡œë“œë  ì²­í¬ ê³„ì‚°
			target_chunk_pos = Global.world_to_chunk(pos_vec)
			if log_diag:
				print("[DIAG] Save data found. Target chunk: %s" % target_chunk_pos)

	# 2. [FIX] í”Œë ˆì´ì–´ê°€ ì„œ ìˆì„ 'ê·¸ ì²­í¬'ê°€ ì¤€ë¹„ë  ë•Œê¹Œì§€ ëŒ€ê¸°
	# (ê¸°ì¡´ì—ëŠ” ë¬´ì¡°ê±´ (0,0)ë§Œ ê¸°ë‹¤ë ¸ìŒ)
	await _wait_for_spawn_chunk_collision(target_chunk_pos)

	# ë¬¼ë¦¬ ì—”ì§„ ì•ˆì •í™” ëŒ€ê¸°
	await get_tree().physics_frame
	await get_tree().physics_frame

	# 3. í”Œë ˆì´ì–´ ìœ„ì¹˜ ë³µì›
	if player:
		if not player_data.is_empty() and player.has_method("apply_save_data"):
			player.apply_save_data(player_data)
			if log_diag:
				print("[DIAG] Player position restored from save")
		else:
			_snap_player_to_ground()

	await _verify_floor_with_raycast_retry()

	# ë¬¼ë¦¬ ë²„ë¸” ì´ˆê¸°í™”
	if player:
		_physics_bubble_center = Global.world_to_chunk(player.global_position)
	
	# ë¡œë”© í™”ë©´ ì¢…ë£Œ ë° ì ê¸ˆ í•´ì œ
	if _loading_screen and is_instance_valid(_loading_screen):
		_loading_screen.finish_loading()
		_loading_screen = null

	if player and player.has_method("unlock"):
		player.unlock()
	
	if log_diag:
		print("[DIAG] Game starting!")

## Phase 2-8 Critical Fix: ìŠ¤í° ì²­í¬ ì¶©ëŒì²´ ëŒ€ê¸° (ì‹¤ì œ ë¬¼ë¦¬ ë…¸ë“œ ê²€ì¦)
func _wait_for_spawn_chunk_collision(target_chunk: Vector2i = Vector2i(0, 0)) -> int:
	var start_chunk_key := target_chunk
	var total_wait_frames: int = 0

	if log_diag:
		print("[DIAG] Waiting for target chunk %s collision..." % target_chunk)

	# 1. ì²­í¬ê°€ active_chunksì— ì¡´ì¬í•  ë•Œê¹Œì§€ ëŒ€ê¸°
	while not active_chunks.has(start_chunk_key):
		await get_tree().process_frame
		total_wait_frames += 1
		if total_wait_frames % 10 == 0 and log_diag:
			print("[DIAG] Waiting for chunk (0,0) to exist... frame %d" % total_wait_frames)

	var start_chunk: VoxelChunk = active_chunks[start_chunk_key]
	if log_diag:
		print("[DIAG] Chunk (0,0) found after %d frames" % total_wait_frames)

	# 2. ìŠ¤ë ˆë“œ ì‘ì—… ì™„ë£Œ ëŒ€ê¸°
	var thread_wait: int = 0
	while start_chunk.is_thread_running():
		await get_tree().process_frame
		thread_wait += 1
		total_wait_frames += 1
		if thread_wait % 10 == 0 and log_diag:
			print("[DIAG] Waiting for thread to complete... frame %d" % thread_wait)

	if log_diag:
		print("[DIAG] Thread completed after %d frames" % thread_wait)

	# 3. ì¶©ëŒì²´ ê°•ì œ ìƒì„± (Robust ë²„ì „ ì‚¬ìš©)
	if not start_chunk.has_collision() or not start_chunk.has_physics_body():
		if log_diag:
			print("[DIAG] Forcing robust collision creation...")
		start_chunk.create_collision_robust()

		# deferred í˜¸ì¶œì´ ì™„ë£Œë  ë•Œê¹Œì§€ ëŒ€ê¸°
		await get_tree().process_frame
		await get_tree().process_frame

	# 4. ì‹¤ì œ ë¬¼ë¦¬ ë…¸ë“œê°€ ì”¬ íŠ¸ë¦¬ì— ì¡´ì¬í•˜ëŠ”ì§€ í™•ì¸
	var collision_wait_frames: int = 0
	const MAX_WAIT_FRAMES: int = 120  # 2ì´ˆ

	while not start_chunk.has_physics_body():
		await get_tree().process_frame
		collision_wait_frames += 1
		total_wait_frames += 1

		if collision_wait_frames % 10 == 0:
			if log_diag:
				print("[DIAG] Waiting for physics body... frame %d" % collision_wait_frames)
				print("[DIAG]   has_collision(): %s" % start_chunk.has_collision())
				print("[DIAG]   has_physics_body(): %s" % start_chunk.has_physics_body())

			# 10í”„ë ˆì„ë§ˆë‹¤ ì¬ì‹œë„
			start_chunk.create_collision_robust()
			await get_tree().process_frame

		if collision_wait_frames >= MAX_WAIT_FRAMES:
			if log_diag:
				print("[DIAG] WARNING: Physics body timeout! Emergency creation...")
			# ìµœí›„ì˜ ìˆ˜ë‹¨: ì§ì ‘ ìƒì„±
			_emergency_create_collision(start_chunk)
			await get_tree().physics_frame
			await get_tree().physics_frame
			break

	if log_diag:
		print("[DIAG] Physics body ready after %d collision-wait frames" % collision_wait_frames)
		print("[DIAG] Final has_physics_body(): %s" % start_chunk.has_physics_body())

	# 5. ë¬¼ë¦¬ ì—”ì§„ì— ë“±ë¡ë  ì‹œê°„ í™•ë³´
	for i in range(3):
		await get_tree().physics_frame

	return total_wait_frames


## ê¸´ê¸‰ ì¶©ëŒì²´ ìƒì„± (ìµœí›„ì˜ ìˆ˜ë‹¨)
func _emergency_create_collision(chunk: VoxelChunk) -> void:
	if log_diag:
		print("[DIAG] _emergency_create_collision called")

	# MeshInstance3D ì°¾ê¸°
	var mi: MeshInstance3D = null
	for child in chunk.get_children():
		if child is MeshInstance3D:
			mi = child
			break

	if mi == null or mi.mesh == null:
		if log_diag:
			print("[DIAG] Emergency: No MeshInstance3D!")
		return

	# ê¸°ì¡´ ì¶©ëŒ ë…¸ë“œ ì œê±°
	for child in mi.get_children():
		if child is StaticBody3D or child.name.contains("col"):
			child.queue_free()

	# ê°•ì œë¡œ ì¶©ëŒì²´ ìƒì„±
	mi.create_trimesh_collision()
	if log_diag:
		print("[DIAG] Emergency collision created")


## Critical Fix: RayCast ê²€ì¦ (ì‹¤íŒ¨ ì‹œ ìµœëŒ€ 10í”„ë ˆì„ ì¬ì‹œë„)
func _verify_floor_with_raycast_retry() -> bool:
	const MAX_RETRIES: int = 10

	for attempt in range(MAX_RETRIES):
		var result: bool = _verify_floor_with_raycast()

		if result:
			if log_diag:
				print("[DIAG] RayCast SUCCESS on attempt %d" % (attempt + 1))
			return true

		if log_diag:
			print("[DIAG] RayCast attempt %d failed, waiting..." % (attempt + 1))
		await get_tree().physics_frame

	if log_diag:
		print("[DIAG] RayCast FAILED after %d attempts" % MAX_RETRIES)
	return false


## Phase 2-8 Diagnostic: RayCastë¡œ ì‹¤ì œ ë¬¼ë¦¬ ë°”ë‹¥ ê²€ì¦
func _verify_floor_with_raycast() -> bool:
	if not player:
		if log_diag:
			print("[DIAG] RayCast verification skipped: No player")
		return false

	var space_state: PhysicsDirectSpaceState3D = get_world_3d().direct_space_state
	if not space_state:
		if log_diag:
			print("[DIAG] RayCast verification skipped: No space state")
		return false

	# í”Œë ˆì´ì–´ ìœ„ì¹˜ì—ì„œ ì•„ë˜ë¡œ ë ˆì´ìºìŠ¤íŠ¸
	var ray_start: Vector3 = player.global_position + Vector3.UP * 0.5
	var ray_end: Vector3 = player.global_position + Vector3.DOWN * 10.0

	var query := PhysicsRayQueryParameters3D.create(ray_start, ray_end)
	query.exclude = [player.get_rid()]

	var result: Dictionary = space_state.intersect_ray(query)

	if result:
		if log_diag:
			print("[DIAG] RayCast Floor Detected: TRUE at %s" % result.position)
			print("[DIAG] RayCast Hit Collider: %s" % result.collider)
			print("[DIAG] RayCast Distance from player: %.2f" % (player.global_position.y - result.position.y))
		return true
	else:
		if log_diag:
			print("[DIAG] RayCast Floor Detected: FALSE (NO COLLISION FOUND!)")
			print("[DIAG] RayCast was from %s to %s" % [ray_start, ray_end])

			# ì¶”ê°€ ì§„ë‹¨: í•´ë‹¹ ìœ„ì¹˜ì˜ ì²­í¬ ìƒíƒœ í™•ì¸
			var player_chunk_pos: Vector2i = Global.world_to_chunk(player.global_position)
			print("[DIAG] Player is in chunk: %s" % player_chunk_pos)

			if active_chunks.has(player_chunk_pos):
				var chunk: VoxelChunk = active_chunks[player_chunk_pos]
				print("[DIAG] That chunk has_collision: %s" % chunk.has_collision())
				print("[DIAG] That chunk has_physics_body: %s" % chunk.has_physics_body())
				print("[DIAG] That chunk is_thread_running: %s" % chunk.is_thread_running())

				# í•´ë‹¹ ì²­í¬ì˜ ìì‹ ë…¸ë“œ ëª©ë¡ ì¶œë ¥
				print("[DIAG] Chunk children: %s" % chunk.get_children())

				# MeshInstance3Dì˜ ìì‹ë„ í™•ì¸
				for child in chunk.get_children():
					if child is MeshInstance3D:
						print("[DIAG] MeshInstance3D children: %s" % child.get_children())
			else:
				print("[DIAG] WARNING: Player's chunk does not exist in active_chunks!")

		return false


## Phase 2-8 Hotfix: í”Œë ˆì´ì–´ë¥¼ ì§€ë©´ì— ê°•ì œ ìŠ¤ëƒ…
func _snap_player_to_ground() -> void:
	# ìŠ¤í° ìœ„ì¹˜ (ì²­í¬ 0,0ì˜ ì¤‘ì•™)
	var spawn_x: float = float(VoxelChunk.CHUNK_SIZE) / 2.0
	var spawn_z: float = float(VoxelChunk.CHUNK_SIZE) / 2.0

	# ìˆ˜í•™ì  ì§€í˜• ë†’ì´ ê³„ì‚° (ë…¸ì´ì¦ˆ ì—­ì‚°)
	var ground_height: float = get_terrain_height(spawn_x, spawn_z)

	# í”Œë ˆì´ì–´ë¥¼ ì§€ë©´ ìœ„ë¡œ í…”ë ˆí¬íŠ¸ (+3.0 ì—¬ìœ )
	player.global_position = Vector3(spawn_x, ground_height + 3.0, spawn_z)

	# ì†ë„ ì´ˆê¸°í™” (ë‚™í•˜ ê°€ì† ì œê±°)
	player.velocity = Vector3.ZERO

	# ë°”ë‹¥ ì°©ì§€ ìƒíƒœ ê°±ì‹ 
	player.move_and_slide()

	print("[WorldGenerator] Player snapped to ground at height: %.1f" % ground_height)


## ë…¸ì´ì¦ˆ ê¸°ë°˜ ì§€í˜• ë†’ì´ ê³„ì‚° (VoxelChunkì™€ ë™ì¼í•œ ê³µì‹)
func get_terrain_height(world_x: float, world_z: float) -> float:
	if noise_base == null or noise_mountain == null:
		return 20.0  # ê¸°ë³¸ê°’

	var h1: float = noise_base.get_noise_2d(world_x, world_z)
	var h2: float = noise_mountain.get_noise_2d(world_x, world_z)

	# VoxelChunk._generate_voxel_data()ì™€ ë™ì¼í•œ ê³µì‹
	var height: float = (h1 * 20.0 + 20.0) + (h2 * 4.0)
	height = clampf(height, 1.0, float(VoxelChunk.CHUNK_HEIGHT - 1))

	return height


## ì´ˆê¸° ë¡œë”© ì¤‘ì¸ì§€ í™•ì¸
func is_initial_loading() -> bool:
	return _is_initial_loading


# ================================
# Global Voxel Lookup
# ================================

## ê¸€ë¡œë²Œ ì¢Œí‘œì—ì„œ ë¸”ë¡ì´ ì†”ë¦¬ë“œì¸ì§€ í™•ì¸
func is_block_solid_global(global_pos: Vector3i) -> bool:
	var chunk_pos := Vector2i(
		floori(float(global_pos.x) / VoxelChunk.CHUNK_SIZE),
		floori(float(global_pos.z) / VoxelChunk.CHUNK_SIZE)
	)

	if not active_chunks.has(chunk_pos):
		return false

	var local_x: int = global_pos.x - (chunk_pos.x * VoxelChunk.CHUNK_SIZE)
	var local_z: int = global_pos.z - (chunk_pos.y * VoxelChunk.CHUNK_SIZE)
	var local_pos := Vector3i(local_x, global_pos.y, local_z)

	var chunk: VoxelChunk = active_chunks[chunk_pos]
	return chunk.has_voxel(local_pos)


## ì²­í¬ ì¢Œí‘œì™€ ë¡œì»¬ ì¢Œí‘œë¥¼ ê¸€ë¡œë²Œ ì¢Œí‘œë¡œ ë³€í™˜
static func to_global_pos(chunk_pos: Vector2i, local_pos: Vector3i) -> Vector3i:
	return Vector3i(
		chunk_pos.x * VoxelChunk.CHUNK_SIZE + local_pos.x,
		local_pos.y,
		chunk_pos.y * VoxelChunk.CHUNK_SIZE + local_pos.z
	)


# ================================
# Player Tracking
# ================================

func _update_player_chunk() -> void:
	var current_chunk: Vector2i = Global.world_to_chunk(player.global_position)

	if current_chunk == last_player_chunk:
		return

	last_player_chunk = current_chunk
	_calculate_chunks_to_load(current_chunk)
	_calculate_chunks_to_unload(current_chunk)


func _calculate_chunks_to_load(center: Vector2i) -> void:
	var render_dist: int = Global.RENDER_DISTANCE

	for x in range(center.x - render_dist, center.x + render_dist + 1):
		for z in range(center.y - render_dist, center.y + render_dist + 1):
			var chunk_pos := Vector2i(x, z)
			var dist: float = Vector2(center - chunk_pos).length()

			if dist > render_dist:
				continue
			if active_chunks.has(chunk_pos):
				continue
			if chunk_pos in chunk_load_queue:
				continue

			_insert_to_load_queue(chunk_pos, dist)


func _insert_to_load_queue(chunk_pos: Vector2i, distance: float) -> void:
	var insert_idx: int = 0
	for i in range(chunk_load_queue.size()):
		var existing_pos: Vector2i = chunk_load_queue[i]
		var existing_dist: float = Vector2(last_player_chunk - existing_pos).length()
		if distance < existing_dist:
			break
		insert_idx = i + 1

	chunk_load_queue.insert(insert_idx, chunk_pos)


func _calculate_chunks_to_unload(center: Vector2i) -> void:
	var render_dist: int = Global.RENDER_DISTANCE
	var unload_dist: float = render_dist + 1.5

	for chunk_pos in active_chunks.keys():
		var dist: float = Vector2(center - chunk_pos).length()
		if dist > unload_dist:
			if chunk_pos not in chunk_unload_queue:
				chunk_unload_queue.append(chunk_pos)


# ================================
# Queue Processing
# ================================

## í˜„ì¬ ìµœëŒ€ ë™ì‹œ ì‹¤í–‰ ì‘ì—… ìˆ˜ ë°˜í™˜
## ì´ˆê¸° ë¡œë”© ì‹œ CPU ì½”ì–´ ìµœëŒ€ í™œìš©, ì´í›„ì—ëŠ” ë°°ê²½ ì‘ì—… ì œí•œ
func _get_current_max_tasks() -> int:
	if _is_initial_loading:
		return maxi(1, OS.get_processor_count() - 1)
	return MAX_CONCURRENT_BACKGROUND_TASKS


func _process_load_queue() -> void:
	var max_tasks: int = _get_current_max_tasks()

	# â˜… ìŠ¤ë ˆë“œ í’€ í¬í™” ì‹œ ìƒì„± ì¤‘ë‹¨ (ì—¬ìœ  ìŠ¤ë ˆë“œ í™•ë³´)
	if _current_background_tasks >= max_tasks:
		return

	while chunk_load_queue.size() > 0 and chunks_generated_this_frame < Global.CHUNKS_PER_FRAME:
		# â˜… ë£¨í”„ ë‚´ì—ì„œë„ ì²´í¬ (í•œ í”„ë ˆì„ì— ì—¬ëŸ¬ ê°œ ìƒì„± ì‹œ)
		if _current_background_tasks >= max_tasks:
			break
		
		var chunk_pos: Vector2i = chunk_load_queue.pop_front()

		if active_chunks.has(chunk_pos):
			continue

		var dist: float = Vector2(last_player_chunk - chunk_pos).length()
		if dist > Global.RENDER_DISTANCE:
			continue

		_create_chunk(chunk_pos)
		chunks_generated_this_frame += 1

		if not _is_initial_loading:
			_queue_neighbor_mesh_updates(chunk_pos)


func _process_unload_queue() -> void:
	var max_unloads: int = 4
	var unloaded: int = 0

	while chunk_unload_queue.size() > 0 and unloaded < max_unloads:
		var chunk_pos: Vector2i = chunk_unload_queue.pop_front()

		var dist: float = Vector2(last_player_chunk - chunk_pos).length()
		if dist <= Global.RENDER_DISTANCE:
			continue

		# ì‚­ì œ ì „ì— ë©”ì‰¬ íì—ì„œ ì œê±°
		_remove_from_mesh_queue(chunk_pos)

		_remove_chunk(chunk_pos)
		unloaded += 1

		# ì¸ì ‘ ì²­í¬ ë©”ì‰¬ ì—…ë°ì´íŠ¸ ìš”ì²­
		_queue_neighbor_mesh_updates(chunk_pos)


# ================================
# Chunk Management
# ================================

func _create_chunk(chunk_pos: Vector2i) -> void:
	if active_chunks.has(chunk_pos):
		return

	# 1. ìºì‹œì—ì„œ ë³µì› ì‹œë„
	if _chunk_cache.has(chunk_pos):
		var chunk: VoxelChunk = _chunk_cache[chunk_pos]
		_chunk_cache.erase(chunk_pos)
		active_chunks[chunk_pos] = chunk
		chunk.visible = true

		var dist: float = Vector2(last_player_chunk - chunk_pos).length()
		if dist <= Global.PHYSICS_DISTANCE:
			chunk.enable_collision()
		return

	# 2. Phase 5: ì €ì¥ íŒŒì¼ í™•ì¸
	if SaveManager.has_saved_chunk(chunk_pos):
		_load_chunk_from_file(chunk_pos)
		return

	# 3. ë…¸ì´ì¦ˆ ìƒì„± (ê¸°ì¡´ ë¡œì§)
	_generate_new_chunk(chunk_pos)


## Phase 5: íŒŒì¼ì—ì„œ ì²­í¬ ë¡œë“œ
func _load_chunk_from_file(chunk_pos: Vector2i) -> void:
	var voxel_data := SaveManager.load_chunk_sync(chunk_pos)
	if voxel_data.is_empty():
		# íŒŒì¼ ì†ìƒ ì‹œ ë…¸ì´ì¦ˆ ì¬ìƒì„±
		if log_perf:
			print("[LOAD_FILE] Chunk %s file corrupted, regenerating" % chunk_pos)
		_generate_new_chunk(chunk_pos)
		return

	var chunk := VoxelChunk.new()
	add_child(chunk)

	chunk.chunk_position = chunk_pos
	chunk.position = Vector3(chunk_pos.x * VoxelChunk.CHUNK_SIZE, 0, chunk_pos.y * VoxelChunk.CHUNK_SIZE)
	chunk.set_voxel_data_from_file(voxel_data)

	# ë°°ê²½ ì‘ì—… ì¹´ìš´íŠ¸
	_current_background_tasks += 1
	if not chunk.mesh_thread_completed.is_connected(_on_background_task_completed):
		chunk.mesh_thread_completed.connect(_on_background_task_completed, CONNECT_ONE_SHOT)

	# ë©”ì‰¬ ë¹Œë“œ (ìŠ¤ë ˆë“œ)
	var needs_collision := _is_physics_distance(chunk_pos)
	chunk.start_threaded_mesh_update(needs_collision)

	active_chunks[chunk_pos] = chunk

	if log_perf:
		print("[LOAD_FILE] Chunk %s loaded from file" % chunk_pos)


## ë…¸ì´ì¦ˆ ê¸°ë°˜ ìƒˆ ì²­í¬ ìƒì„±
func _generate_new_chunk(chunk_pos: Vector2i) -> void:
	var chunk := VoxelChunk.new()
	add_child(chunk)

	var needs_collision := _is_physics_distance(chunk_pos)

	_current_background_tasks += 1
	if not chunk.mesh_thread_completed.is_connected(_on_background_task_completed):
		chunk.mesh_thread_completed.connect(_on_background_task_completed, CONNECT_ONE_SHOT)

	chunk.generate_chunk_threaded(chunk_pos, noise_base, noise_mountain, needs_collision)
	active_chunks[chunk_pos] = chunk


## ë¬¼ë¦¬ ê±°ë¦¬ ë‚´ í™•ì¸ í—¬í¼
func _is_physics_distance(chunk_pos: Vector2i) -> bool:
	if player == null:
		return true  # ì´ˆê¸° ë¡œë”© ì‹œ ì•ˆì „í•˜ê²Œ true
	var player_chunk := Global.world_to_chunk(player.global_position)
	var dist := Vector2(player_chunk - chunk_pos).length()
	return dist <= Global.PHYSICS_DISTANCE


func _remove_chunk(chunk_pos: Vector2i) -> void:
	if not active_chunks.has(chunk_pos):
		return

	var chunk: VoxelChunk = active_chunks[chunk_pos]

	# Phase 5: Dirty ì²­í¬ëŠ” ì €ì¥
	if chunk.is_dirty():
		SaveManager.save_chunk_async(chunk_pos, chunk.get_voxel_data())
		chunk.mark_clean()
		if log_perf:
			print("[SAVE] Chunk %s queued for save (dirty)" % chunk_pos)

	active_chunks.erase(chunk_pos)

	# ìŠ¤ë ˆë“œ ëŒì•„ê°€ëŠ” ì¤‘ì´ê±°ë‚˜ ìºì‹œ ì—¬ìœ  ìˆìœ¼ë©´ ìºì‹œë¡œ
	if chunk.is_thread_running() or _chunk_cache.size() < MAX_CACHED_CHUNKS:
		_chunk_cache[chunk_pos] = chunk
		chunk.visible = false
		chunk.disable_collision()
	else:
		chunk.queue_free()


## â˜… ì¸ì ‘ ì²­í¬ ë©”ì‰¬ ì—…ë°ì´íŠ¸ ìš”ì²­ (í ì‹œìŠ¤í…œ)
func _queue_neighbor_mesh_updates(center_chunk: Vector2i) -> void:
	for offset in NEIGHBOR_OFFSETS:
		var neighbor_pos: Vector2i = center_chunk + offset

		if active_chunks.has(neighbor_pos):
			var neighbor: VoxelChunk = active_chunks[neighbor_pos]
			request_mesh_update(neighbor)


## ì²­í¬ê°€ ì‚­ì œë  ë•Œ ë©”ì‰¬ íì—ì„œ ì œê±°
func _remove_from_mesh_queue(chunk_pos: Vector2i) -> void:
	if not active_chunks.has(chunk_pos):
		return

	var chunk: VoxelChunk = active_chunks[chunk_pos]
	var idx: int = _mesh_update_queue.find(chunk)
	if idx >= 0:
		_mesh_update_queue.remove_at(idx)


# ================================
# Utility
# ================================

func get_spawn_position() -> Vector3:
	var spawn_chunk := Vector2i(0, 0)
	if not active_chunks.has(spawn_chunk):
		_create_chunk(spawn_chunk)

	var center_x: int = VoxelChunk.CHUNK_SIZE / 2
	var center_z: int = VoxelChunk.CHUNK_SIZE / 2

	if active_chunks.has(spawn_chunk):
		var chunk: VoxelChunk = active_chunks[spawn_chunk]
		for y in range(VoxelChunk.CHUNK_HEIGHT - 1, -1, -1):
			if chunk.has_voxel(Vector3i(center_x, y, center_z)):
				return Vector3(center_x, y + 2, center_z)

	return Vector3(center_x, 50, center_z)


func get_active_chunk_count() -> int:
	return active_chunks.size()


func get_queue_sizes() -> Dictionary:
	return {
		"load": chunk_load_queue.size(),
		"unload": chunk_unload_queue.size(),
		"mesh_update": _mesh_update_queue.size(),
		"mesh_updating": _is_mesh_updating,
		"chunk_apply": _chunk_apply_queue.size(),
		"collision_request": _collision_request_queue.size(),
		"initial_loading": _is_initial_loading,
		"initial_progress": [_initial_chunks_finalized, _initial_chunks_to_load],
		"physics_bubble_center": _physics_bubble_center
	}


## Phase 2-9: í˜„ì¬ ë¬¼ë¦¬ ë²„ë¸” ìƒíƒœ ë°˜í™˜
func get_physics_bubble_info() -> Dictionary:
	var chunks_with_physics: int = 0
	var chunks_without_physics: int = 0

	for chunk in active_chunks.values():
		if chunk.has_physics_body():
			chunks_with_physics += 1
		else:
			chunks_without_physics += 1

	return {
		"center": _physics_bubble_center,
		"radius": Global.PHYSICS_DISTANCE,
		"with_physics": chunks_with_physics,
		"without_physics": chunks_without_physics
	}


## Phase 2-6: ì¶©ëŒì²´ í†µê³„ ë°˜í™˜
func get_collision_stats() -> Dictionary:
	var with_collision: int = 0
	var without_collision: int = 0

	for chunk in active_chunks.values():
		if chunk.has_collision():
			with_collision += 1
		else:
			without_collision += 1

	return {
		"with_collision": with_collision,
		"without_collision": without_collision,
		"total": active_chunks.size()
	}


## ìºì‹œ ì •ë¦¬: ìŠ¤ë ˆë“œ ì™„ë£Œëœ ì²­í¬ ì¤‘ ì´ˆê³¼ë¶„ ì‚­ì œ
func _cleanup_chunk_cache() -> void:
	if _chunk_cache.size() <= MAX_CACHED_CHUNKS:
		return
	
	var to_remove: Array[Vector2i] = []
	for pos in _chunk_cache.keys():
		var chunk: VoxelChunk = _chunk_cache[pos]
		if not chunk.is_thread_running():
			to_remove.append(pos)
			if _chunk_cache.size() - to_remove.size() <= MAX_CACHED_CHUNKS:
				break
	
	for pos in to_remove:
		var chunk: VoxelChunk = _chunk_cache[pos]
		_chunk_cache.erase(pos)
		chunk.queue_free()

# ================================
# Block Modification
# ================================
func place_block(global_pos: Vector3i, block_type: int = 1) -> void:
	var t0 := Time.get_ticks_usec()  # ì¶”ê°€
	
	var chunk_pos := Vector2i(
		floori(float(global_pos.x) / VoxelChunk.CHUNK_SIZE),
		floori(float(global_pos.z) / VoxelChunk.CHUNK_SIZE)
	)
	
	if not active_chunks.has(chunk_pos):
		return
	
	var chunk: VoxelChunk = active_chunks[chunk_pos]
	var local_pos := _global_to_local(global_pos, chunk_pos)

	chunk.set_voxel(local_pos, block_type)
	var t1 := Time.get_ticks_usec()

	_refresh_chunk_and_neighbors(chunk_pos, local_pos)
	var t2 := Time.get_ticks_usec()  # ì¶”ê°€
	
	# ì¶”ê°€: íƒ€ì´ë° ë¡œê·¸
	print("[PLACE] setup: %d Âµs, refresh: %d Âµs, total: %d Âµs" % [t1 - t0, t2 - t1, t2 - t0])

func break_block(global_pos: Vector3i) -> void:
	var t0 := Time.get_ticks_usec()  # ì¶”ê°€
	
	var chunk_pos := Vector2i(
		floori(float(global_pos.x) / VoxelChunk.CHUNK_SIZE),
		floori(float(global_pos.z) / VoxelChunk.CHUNK_SIZE)
	)
	
	if not active_chunks.has(chunk_pos):
		if log_debug:
			print("[BLOCK] Chunk %s not in active_chunks" % chunk_pos)
		return
	
	var chunk: VoxelChunk = active_chunks[chunk_pos]
	var local_pos := _global_to_local(global_pos, chunk_pos)
	
	# ë””ë²„ê·¸: ìŠ¤ë ˆë“œ ìƒíƒœ í™•ì¸
	if log_debug:
		print("[BLOCK] Breaking at global: %s" % global_pos)
		print("[BLOCK] Chunk %s thread_running: %s" % [chunk_pos, chunk.is_thread_running()])

	if not chunk.has_voxel(local_pos):
		if log_debug:
			print("[BLOCK] Already empty at %s" % local_pos)
		return

	chunk.erase_voxel(local_pos)
	var t1 := Time.get_ticks_usec()
	
	if log_debug:
		print("[BLOCK] Refreshing chunk %s, local: %s" % [chunk_pos, local_pos])
	
	_refresh_chunk_and_neighbors(chunk_pos, local_pos)
	var t2 := Time.get_ticks_usec()  # ì¶”ê°€
	
	# ì¶”ê°€: íƒ€ì´ë° ë¡œê·¸
	print("[BREAK] setup: %d Âµs, refresh: %d Âµs, total: %d Âµs" % [t1 - t0, t2 - t1, t2 - t0])

func _global_to_local(global_pos: Vector3i, chunk_pos: Vector2i) -> Vector3i:
	return Vector3i(
		global_pos.x - chunk_pos.x * VoxelChunk.CHUNK_SIZE,
		global_pos.y,
		global_pos.z - chunk_pos.y * VoxelChunk.CHUNK_SIZE
	)


func _refresh_chunk_and_neighbors(chunk_pos: Vector2i, local_pos: Vector3i) -> void:
	if log_debug:
		print("[BLOCK] Refreshing chunk %s, local: %s" % [chunk_pos, local_pos])
	
	var chunk: VoxelChunk = active_chunks[chunk_pos]
	chunk.start_threaded_mesh_update(chunk.has_physics_body(), true)  # â˜… priority=true
	
	# ê²½ê³„ ë¸”ë¡ì´ë©´ ì¸ì ‘ ì²­í¬ë„ ê°±ì‹  (ì´ì›ƒì€ ì¼ë°˜ ìš°ì„ ìˆœìœ„)
	if local_pos.x == 0:
		_refresh_neighbor(chunk_pos + Vector2i(-1, 0))
	if local_pos.x == VoxelChunk.CHUNK_SIZE - 1:
		_refresh_neighbor(chunk_pos + Vector2i(1, 0))
	if local_pos.z == 0:
		_refresh_neighbor(chunk_pos + Vector2i(0, -1))
	if local_pos.z == VoxelChunk.CHUNK_SIZE - 1:
		_refresh_neighbor(chunk_pos + Vector2i(0, 1))


func _refresh_neighbor(neighbor_pos: Vector2i) -> void:
	if active_chunks.has(neighbor_pos):
		var neighbor: VoxelChunk = active_chunks[neighbor_pos]
		neighbor.start_threaded_mesh_update(neighbor.has_physics_body())

## Thread-safe ê¸€ë¡œë²Œ ì¢Œí‘œ ë¸”ë¡ í™•ì¸
func is_block_solid_threadsafe(global_pos: Vector3i) -> bool:
	var chunk_pos := Vector2i(
		floori(float(global_pos.x) / VoxelChunk.CHUNK_SIZE),
		floori(float(global_pos.z) / VoxelChunk.CHUNK_SIZE)
	)
	
	if not active_chunks.has(chunk_pos):
		return false
	
	var local_x: int = global_pos.x - (chunk_pos.x * VoxelChunk.CHUNK_SIZE)
	var local_z: int = global_pos.z - (chunk_pos.y * VoxelChunk.CHUNK_SIZE)
	var local_pos := Vector3i(local_x, global_pos.y, local_z)
	
	var chunk: VoxelChunk = active_chunks[chunk_pos]
	return chunk.has_voxel_threadsafe(local_pos)

func request_priority_chunk_apply(chunk: VoxelChunk) -> void:
	if chunk not in _priority_apply_queue:
		_priority_apply_queue.append(chunk)
